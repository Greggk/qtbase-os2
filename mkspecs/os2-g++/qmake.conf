#
# qmake configuration for os2-g++
#
# Written for GCC and OpenWatcom WLINK and assumes the Unix-like environment
# provided by bww bitwise works GmbH in their OS/2 RPM repositories.
#

include(../common/gcc-base.conf)
include(../common/g++-base.conf)

# modifications to gcc-base.conf and g++-base.conf

MAKEFILE_GENERATOR      = UNIX
QMAKE_PLATFORM          = os2
CONFIG                 += debug_and_release debug_and_release_target
QMAKE_COMPILER_DEFINES +=

QMAKE_LEX               = flex
QMAKE_LEXFLAGS          =
QMAKE_YACC              = bison -y
QMAKE_YACCFLAGS         = -d

# Need i486 for __sync_fetch_and_sub etc, i686 - for __atomic_load_8 etc.
QMAKE_CFLAGS            = -Zomf -march=i686 -mtune=i686
QMAKE_CXXFLAGS          = $$QMAKE_CFLAGS

QMAKE_CFLAGS_SSE2      += -mstackrealign

# -ffunction-sections is not supported by GCC/2
QMAKE_CFLAGS_SPLIT_SECTIONS   =
QMAKE_CXXFLAGS_SPLIT_SECTIONS = $$QMAKE_CFLAGS_SPLIT_SECTIONS

QMAKE_INCDIR            =

QMAKE_LFLAGS            = -Zomf -Zstack 0x2000 -Zhigh-mem -Zlinker \"DISABLE 1121\"
QMAKE_LFLAGS_CONSOLE    = -Zlinker /PM:VIO
QMAKE_LFLAGS_WINDOWS    = -Zlinker /PM:PM -Zno-fork
QMAKE_LFLAGS_DLL        = -Zdll
QMAKE_LFLAGS_MAP        = -Zmap

QMAKE_LIBS              = -lcx

QMAKE_PREFIX_SHLIB      =
QMAKE_EXTENSION_SHLIB   = dll
QMAKE_PREFIX_STATICLIB  =
QMAKE_EXTENSION_STATICLIB = lib

QMAKE_LIB               = emxomfar r
QMAKE_RC                = rc

QMAKE_STRIP             = emxomfstrip
